### 1.1.7 实例：采用牛顿法求平方根

上面介绍的过程都很像常规的数学函数，它们描述的是如何根据一个或者几个参数去确定一个值。然而，在数学的函数和计算机的过程之间有一个重要差异，那就是，这一过程还必须是有效可行的。

作为目前情况下的一个实例，现在我们来考虑求平方根的问题。我们可以将平方根函数定义为：

\\[\sqrt{x}=那样的y，使得y \geq 0而且y^2=x\\]

这就描述出了一个完全正统的数学函数，我们可以利用它去判断某个数是否为另一个数的平方根，或根据上面描述，推导出一些有关平方根的一般性事实。然而，在另一方面，这一定义并没有描述一个计算过程，因为它确实没有告诉我们，在给定了一个数之后，如何实际地找到这个数的平方根。即使将这个定义用类似 Lisp 的形式重写一遍也完全无济于事：

```
(define (sqrt x)
  (the y (and (>= y 0)
              (= (square y) x))))
```

这只不过是重新提出了原来的问题。

函数与过程之间的矛盾，不过是在描述一件事情的特征，与描述如何去做这件事情之间的普遍性差异的一个具体反映。换一种说法，人们有时也将它说成是说明性的知识与行动性的知识之间的差异。在数学里，人们通常关心的是说明性的描述（是什么），而在计算机科学里，人们则通常关心行动性的描述（怎么做）。

计算机如何算出平方根呢？最常用的就是牛顿的逐步逼近方法。这一方法告诉我们，如果对 \\(x\\) 的平方根的值有了一个猜测 \\(y\\) ，那么就可以通过执行一个简单操作去得到一个更好的猜测：只需要求出 \\(y\\) 和 \\(\frac{x}{y}\\) 的平均值（它更接近实际的平方根值）。例如，可以用这种方式去计算 \\(2\\) 的平方根，假定初始值是 \\(1\\) ：

| 猜测 | 商 | 平均值 |
|------|----|--------|
|   \\(1\\)  | \\(\frac{2}{1}=2\\)  | \\(\frac{2+1}{2}=1.5\\)    |
|\\(1.5\\)| \\(\frac{2}{1.5}=1.3333\\) | \\(\frac{1.3333+1.5}{2}=1.4167\\) |
|\\(1.4167\\) | \\(\frac{2}{1.4167}=1.4118\\) | \\(\frac{1.4167+1.4118}{2}=1.4142\\) |
| 1.4142 | ... | ... |

继续这一计算过程，我们就能得到对 \\(2\\) 的平方根的越来越好的近似值。

现在，让我们设法用过程的语言来描述这一计算过程。开始时，我们有了被开方数的值（现在需要做的就是算出它的平方根）和一个猜测值。如果猜测值已经足够好了，有关工作也就完成了。如若不然，那么就需要重复上述计算过程去改进猜测值。我们可以将这一基本策略写成下面的过程：

```
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))
```

改进猜测的方式就是求出它与被开方数除以上一个猜测的平均值：

```
(define (improve guess x)
  (average guess (/ x guess)))
```

其中

```
(define (average x y)
  (/ (+ x y) 2))
```

我们还必须说明什么叫做“足够好”。下面的做法只是为了说明问题，它确实不是一个很好的检测方法（参见练习 1.7 ）。这里的想法是，不断改进答案直至它足够接近平方根，使得其平方与被开放数之差小于某个事先确定的误差值（这里用的是 \\(0.001\\)）：

```
(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))
```

最后还需要一种方式来启动整个工作。例如，我们总用 1 作为对任何数的初始猜测值：

```
(define (sqrt x)
  (sqrt-iter 1.0 x))
```

如果把这些定义都送给解释器，我们就可以使用 `sqrt` 了，就像可以使用其他过程一样：

```
(sqrt 9)
3.00009155413138

(sqrt (+ 100 37))
11.704699917758145

(sqrt (+ (sqrt 2) (sqrt 3)))
1.7739279023207892

(square (sqrt 1000))
1000.000369924366
```

这个 `sqrt` 程序也说明，在用于写纯粹的数值计算程序时，至今已介绍的简单程序设计语言已经足以写出可以在其他语言（例如 C 或者 Pascal）中写出的任何东西了。这看起来很让人吃惊，因为这一语言中甚至还没有包括任何迭代结构（循环），它们用于指挥计算机去一遍遍地做某些事情。而在另一方面， `sqrt-iter` 展示了如何不用特殊的迭代结构来实现迭代，其中只需要使用常规的过程调用能力。

**练习 1.6** Alyssa P. Hacker 看不出为什么需要将 `if` 提供为一种特殊形式，她问：“为什么我不能直接通过 `cond` 将它定义为一个常规过程呢？” Alyssa 的朋友 Eva Lu Ator 断言确实可以这样做，并定义了 `if` 的一个新版本：

```
(define (new-if predicate then-clause else-clause)
  (cond (predicate then-clause)
        (else else-clause)))
```

Eva 给 Alyssa 演示她的程序：

```
(new-if (= 2 3) 0 5)
5

(new-if (= 1 1) 0 5)
0
```

她很高兴地用自己的 `new-if` 重写了平方根的程序：

```
(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x)
                     x)))
```

当 Alyssa 试着用这个过程去计算平方根时会发生什么事情呢？请给出解释。

**练习 1.7** 对于确定很小的数的平方根而言，在计算平方根中使用的检测 `good-enough?` 是很不好的。还有，在现实的计算机里，算术运算总是以一定的有限精度进行的。这也会使我们的检测不适合非常大的数的计算。请解释上述论断，用例子说明对很小和很大的数，这种检测都可能失败，实现 `good-enough?` 的另一种策略是监视猜测值在从一次迭代到下一次的变化情况。当改变值相对与猜测值的比率很小时就结束。请设计一个采用这种终止测试方式的平方根过程。对于很大和很小的数，这一方式都能工作吗？

**练习 1.8** 求立方根的牛顿法基于如下事实，如果 \\(y\\) 是 \\(x\\) 的立方根的一个近似值，那么下式将给出一个更好的近似值：

\\[\frac{\frac{x}{y^2} + 2y}{3}\\]

请利用这一公式实现一个类似平方根过程的求立方根的过程。（在 1.3.4 节里，我们将看到如何实现一般性的牛顿法，作为这些求平方根和立方根过程的抽象。）